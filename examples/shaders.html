<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, initial-scale=1">
		<title>Scrollmeister parallax ALL the things</title>
		<style>
			footer {
				text-align: center;
				padding: 1em 0;
			}

			scroll-meister, element-meister, content-meister {
				display: block;
			}

			scroll-meister[fadein] {
				opacity: 0;
				transition: opacity 0.3s ease-in-out;
			}

			select {
				position: fixed;
				left: 1em;
				top: 1em;
			}
		</style>
	</head>
	<body>
		<scroll-meister fadein scroll guides-layout="guides: left 0, right 100%;">
			<element-meister layout="height: 1920 / 1280; guides: left right; spacing: 75vh;" interpolate media="ratio: 1920 / 1280" gl-effect="shader: swirl">
				<img src="assets/images/niilo-isotalo-379496-unsplash.jpg">
			</element-meister>
		</scroll-meister>

		<select>
			<option selected>swirl</option>
			<option>vhs</option>
			<option>pixelize</option>
			<option>hue-rotate</option>
		</select>

		<script type="text/plain" id="swirl">
		precision mediump float;

		uniform float progress;
		uniform sampler2D image;
		uniform vec2 size;

		varying vec2 uv;

		void main() {
			vec2 center = size / 2.0;
			float radius = progress * max(size.x, size.y) / 2.0;
			float angle = progress * 3.14159 * 5.0;

			vec2 coord = uv * size;

			coord -= center;

			float distance = length(coord);

			if (distance < radius) {
				float percent = (radius - distance) / radius;
				float theta = percent * percent * angle;
				float s = sin(theta);
				float c = cos(theta);

				coord = vec2(
					coord.x * c - coord.y * s,
					coord.x * s + coord.y * c
				);
			}

			coord += center;

			gl_FragColor = texture2D(image, coord / size);
		}
		</script>

		<script type="text/plain" id="pixelize">
		precision mediump float;

		uniform float progress;
		uniform vec2 size;
		uniform sampler2D image;

		varying vec2 uv;

		void main() {
			//This way we see the pixels much earlier. We skip the boring part were they're just like a little bigger than original.
			float easedProgress = pow(progress, 0.1);

			//The grid size in whole squares, e.g. 15x7.
			vec2 grid = floor(size * (1.0 - easedProgress) + 0.5);

			//The size of a single pixelated block, e.g. 0.1x0.07 (10%x7% of the texture size).
			vec2 pixelSize = 1.0 / grid;

			//We use the color at the center of the block for all pixels inside of it.
			vec2 transformedUV = (floor(uv / pixelSize) / grid) + (pixelSize / 2.0);

			gl_FragColor = texture2D(image, transformedUV);
		}
		</script>

		<script type="text/plain" id="hue-rotate">
		precision mediump float;

		uniform float progress;
		uniform sampler2D image;
		uniform vec2 resolution;

		varying vec2 uv;

		vec3 rgb2hsv(vec3 c) {
			vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
			vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
			vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

			float d = q.x - min(q.w, q.y);
			float e = 1.0e-10;
			return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
		}


		vec3 hsv2rgb(vec3 c) {
			vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
			vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
			return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
		}

		void main() {
			vec4 color = texture2D(image, uv);

			vec3 hsv = rgb2hsv(color.rgb);

			hsv.x = hsv.x + progress;

			gl_FragColor = vec4(hsv2rgb(hsv), color.a);
		}
		</script>

		<script type="text/plain" id="vhs">
		precision highp float;

		uniform float progress;
		uniform sampler2D image;
		uniform vec2 resolution;

		varying vec2 uv;

		void main() {
			float amount = 0.7;
			float seed = 1.0;
			float lines = 300.0;

			vec2 position = uv;
			float y = floor(lines * position.y) / lines;

			float disf = 0.01 * (cos(position.y * 130.0 + progress * 10.0) + sin(position.y * 183.0 + progress * 80.0));
			float parity = 0.0;

			if(mod(y*lines, 2.0) > 0.5) {
				parity=1.;
			}
			else {
				parity=-1.0;
			}

			float a = smoothstep(0.0, 1.0, progress);

			position.x = amount * a * (y * 0.3 + disf) + position.x + amount * 0.5 * parity * smoothstep(0.6, 0.65, progress) * (sin(position.y * (12.0 + 40.0 * seed)) + smoothstep(0.64, 0.65, progress));

			vec4 colorInput = texture2D(image, position);

			gl_FragColor = colorInput;
		}
		</script>

		<script src="../dist/scrollmeister-extras.js"></script>
		<script>
			document.querySelector('select').addEventListener('change', function() {
				document.querySelector('element-meister[gl-effect]').glEffect.shader = this.value;
			}, false);
		</script>
	</body>
</html>
